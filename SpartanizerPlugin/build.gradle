// Buildscript ################################################################
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.diffplug.gradle:goomph:3.7.2"
  }
}

// Repositories definitions ###################################################
repositories {
	mavenCentral()
	jcenter()
}
repositories {
	flatDir {
		dirs 'lib'
	}
}

// Plugins ####################################################################
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'com.diffplug.gradle.p2.asmaven'

// Imports ####################################################################
apply from: 'eclipse-deps.gradle'


// Java settings ##############################################################
compileJava.options.encoding = 'UTF-8'
compileJava.sourceCompatibility = JavaVersion.VERSION_1_8
compileJava.targetCompatibility = JavaVersion.VERSION_1_8



// P2AsMaven configuration ####################################################
// Downloads required p2 packages, transforms them to maven packages, and
// stores them in a local maven repository in build/p2asmaven/maven
def eclipse_deps_group = 'eclipse-deps'
p2AsMaven {
	group eclipse_deps_group, {
        // the repositories and artifacts to download
        repoEclipse '4.5.2'
		for (dep in project.eclipse_deps) { 
			iu dep
		}		
    }
	group 'eclipse-jgit-deps', {
		repo 'http://download.eclipse.org/egit/updates/'
		iu			'org.eclipse.jgit'
	}
}

// Dependencies ###############################################################
dependencies {
	compile project(':spartan')
	compile project(':fluent.ly')
	compile files('lib/cli-1.02.jar')
	compile group: 'org.jetbrains', name: 'annotations', version: '13.0'
	compile 'junit:junit:4.12'
	compile 'org.hamcrest:hamcrest-junit:2.0.0.0'
	compile "org.mockito:mockito-core:1.+"
	
	// compile all eclipse p2 dependencies
	for (dep in project.eclipse_deps) { 
		compile eclipse_deps_group + ':' + dep + ':+'
	}

	// Jgit??
	compile "eclipse-jgit-deps:org.eclipse.jgit:+"
	
	// https://mvnrepository.com/artifact/org.osgi/org.osgi.framework
	compile group: 'org.osgi', name: 'org.osgi.framework', version: '1.8.0'
}
