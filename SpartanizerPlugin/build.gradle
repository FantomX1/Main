buildscript {
	repositories {
        jcenter()
		// make it easy to test a snapshot version of goomph
		maven {	url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		// grab dependencies from the gradle plugin portal
		maven { url 'https://plugins.gradle.org/m2/' }
		
		mavenCentral()
	}
	// make sure we don't cache stale snapshot versions
	configurations.all {
		resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
	}
	dependencies {
		// a bunch of eclipse stuff
		classpath "com.diffplug.gradle:goomph:${VER_GOOMPH}"
		// creates a targetplatform
		classpath "org.standardout:bnd-platform:${VER_BND_PLATFORM}"
        // shadow, for creating a fat jar
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.0"
	}
}

repositories {
    mavenCentral()
    // local eclipse maven (created by Goomph)
    maven {
        url rootProject.file('target.p2/build/p2asmaven/maven')
    }
}
// we need the maven repo from p2
evaluationDependsOn(':target.p2')

//////////
// JAVA //
//////////
apply plugin: 'java'
compileJava.options.encoding = 'UTF-8'

sourceSets {
	main { java {
		srcDir 'src'
		srcDir 'test'
	} }
	test { java {
		srcDir 'test'
	} }
}
sourceCompatibility = VER_JAVA
targetCompatibility = VER_JAVA

test {
	beforeTest { testDesc ->
		println "Running test: " + testDesc.getClassName() + " - " + testDesc.getName()
	}
}
dependencies {
    // eclipse/p2 dependencies
    def repoDir = project(':target.p2').buildDir.path + "/p2asmaven/maven"

    new File(repoDir + "/eclipse-deps").eachDir {d ->
        if (d.name.contains('tukaani.xz.source')) return // TODO maybe there is a better way to exclude source packages
		if (d.name.contains('jasper.glassfish')) return
		if (d.name.contains('compiler.batch')) return
		compile "eclipse-deps:" + d.name + ":+"
    }

    new File(repoDir + "/eclipse-deps-jgit").eachDir {d ->
        compile "eclipse-deps-jgit:" + d.name + ":+"
    }

    // add SWT and the appropriate platform-native SWT for building and testing
    compile "eclipse-deps:org.eclipse.swt:+"
    compile "eclipse-deps:org.eclipse.swt.${com.diffplug.common.swt.os.SwtPlatform.getNative()}:+"

    // project dependencies
    compile project(':spartan')
    compile project(':fluent.ly')

    // maven dependencies
    compile "junit:junit:${VER_JUNIT}"
    compile "org.mockito:mockito-all:1.9.5"
}

apply plugin: 'osgi'
// configure the OSGi bundle
jar.manifest.attributes(
		'-exportcontents': 'il.org.spartan.*',
		'-removeheaders': 'Bnd-LastModified,Bundle-Name,Created-By,Tool,Private-Package,Require-Capability',
		'-importcontents': '*',
		'Bundle-SymbolicName': project.name,
		'Bundle-RequiredExecutionEnvironment': 'JavaSE-1.8',
		'Require-Capability': 'osgi.ee;filter:="(&(osgi.ee=JavaSE)(version=1.8))"',
		'Bundle-Vendor': 'il.org.spartan',
        'Bundle-Version': '1.0.0',
		'Bundle-License': "http://www.apache.org/licenses/LICENSE-2.0"
)

/////////////
// FAT JAR //
/////////////
apply plugin: 'com.github.johnrengelman.shadow'

def projectDependencies = project.configurations.runtime.getAllDependencies()
shadowJar {
    dependencies {
        exclude(dependency("eclipse-deps:.*:.*"))
    }
}

//////////////////////
// ECLIPSE PROJECTS //
//////////////////////
apply plugin: 'eclipse'
// remove the build folder
apply plugin: 'com.diffplug.gradle.eclipse.excludebuildfolder'
// improve the project deps
apply plugin: 'com.diffplug.gradle.eclipse.projectdeps'
// handle build.properties correctly
apply plugin: 'com.diffplug.gradle.eclipse.buildproperties'
//
eclipse {
	project {
		natures 'org.eclipse.pde.PluginNature'
		natures 'org.eclipse.jdt.core.javanature'
		buildCommand 'org.eclipse.jdt.core.javabuilder'
		buildCommand 'org.eclipse.pde.ManifestBuilder'
		buildCommand 'org.eclipse.pde.SchemaBuilder'
	}
	classpath {
		downloadSources true
		downloadJavadoc true
	}
	jdt {
		sourceCompatibility VER_JAVA
		targetCompatibility VER_JAVA
	}
}
// always create "fresh" projects
tasks.eclipse.dependsOn(cleanEclipse)


/////////////////////////////////////////////////////////
// Root eclipse project for tinkering with build files //
/////////////////////////////////////////////////////////
//apply plugin: 'com.diffplug.gradle.eclipse.resourcefilters'
//eclipseResourceFilters {
//	exclude().folders().name('SpartanizerPlugin/plugin')
//}
